/*
 * Copyright (c) 2022 CodeHead Systems. All rights reserved
 */
// publishing : https://stackoverflow.com/questions/71181790/how-do-you-publish-a-kotlin-artifact-to-maven-central
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven.google.com" } // Google's Maven repository
        gradlePluginPortal() // was jcenter() which is dying
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'org.gretty:gretty:3.0.2'
    }
}

plugins {
    id 'base'
    id 'java-library'
    id 'jacoco'
    id 'idea'
    id 'signing'
    id 'maven-publish'
    id 'jvm-test-suite'
}
java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}
repositories {
    mavenLocal()
    mavenCentral()
    gradlePluginPortal()  // was jcenter() which is dying
    google()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://s3-us-west-2.amazonaws.com/dynamodb-local/release/" }
}

group 'com.codeheadsystems'
version '1.0.6-SNAPSHOT'
//version '1.0.7'
// publish and signing

ext {
    appName = 'codehead-test'
}
base {
    archivesName = "${appName}"
    version = "${version}"
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                runtimeOnly(libs.junit.jupiter.engine)
                implementation(libs.immutables.value.annotations)
                implementation(libs.jackson.annotations)
                annotationProcessor(libs.immutables.value)
            }
        }
    }
}

jacocoTestReport {
    dependsOn test
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/dagger/**.class',
                    '**/Dagger*Builder.class',
                    '**/*Exception.class',
                    '**/*Module.class',
                    '**/*_Factory.class',
                    '**/*_Factory.*class',
                    '**/*InstanceHolder.class',
                    '**/*_Factory$InstanceHolder.class'
            ])
        }))
    }
}
def excludeFiles = [
        '**.dagger.**',
        '**.Dagger*Builder',
        '**.*Exception',
        '**.*Module',
        '**.*_Factory',
        '**.*_Factory*',
        '**.InstanceHolder',
        '**.*_Factory$InstanceHolder'
]

// If you want to override, use this in your package and set. This is the low bar.
jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            excludes = excludeFiles
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
        rule {
            excludes = excludeFiles
            element = 'CLASS'
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 0.7
            }
        }
    }
}

jacocoTestReport.finalizedBy jacocoTestCoverageVerification


dependencies {
    implementation(libs.guava)
    implementation(libs.jackson.core)
    implementation(libs.jackson.databind)
    implementation(libs.jackson.datatype.jdk8)
    implementation(libs.junit.jupiter.api)
    implementation(libs.assertj.core)
    implementation(libs.mockito.junit.jupiter)
    implementation(libs.mockito.core)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = appName
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'CodeHead-Test'
                description = 'Testing utilities'
                url = 'https://github.com/wolpert/codehead-test'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'wolpert'
                        name = 'Ned Wolpert'
                        email = 'ned.wolpert@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wolpert/codehead-test.git'
                    developerConnection = 'scm:git:ssh://github.com/wolpert/codehead-test.git'
                    url = 'https://github.com/wolpert/codehead-test/'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            name = "ossrh"
            credentials(PasswordCredentials)
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
